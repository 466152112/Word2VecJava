apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = 'com.medallia.word4vec'
version = '0.10.2'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.thrift:libthrift:0.9.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.1'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.3'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        //noinspection SpellCheckingInspection
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: "*****", password: "*****")
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: "*****", password: "*****")
      }

      pom.project {
        name 'Word2VecJava'
        packaging 'jar'
        description 'Word2Vec Java Port'
        url 'https://github.com/medallia/Word2VecJava'

        scm {
          connection 'scm:git:git@github.com:medallia/Word2VecJava.git'
          developerConnection 'scm:git:git@github.com:medallia/Word2VecJava.git'
          url 'https://github.com/medallia/Word2VecJava.git'
        }

        licenses {
          license {
            name 'The MIT License'
            url 'http://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'wko'
            name 'Andrew Ko'
            email 'wko@medallia.com'
          }
	  developer {
	    id 'yibinlin'
	    name 'Yibin Lin'
	    email 'yibin@medallia.com'
	  }
	  developer {
	    id 'yfpeng'
	    name 'Yifan Peng'
	    email 'yfpeng@udel.edu'
	  }
	  developer {
	    id 'guerda'
	    name 'Philip Gilli√üen'
	  }
        }
      }
    }
  }
}
